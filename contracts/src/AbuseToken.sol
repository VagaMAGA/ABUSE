// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

/// @title AbuseToken — $A airdrop ERC20 held by Hub for point redemptions
contract AbuseToken {
    string public constant name = "ABUSE";
    string public constant symbol = "A";
    uint8 public constant decimals = 18;

    uint256 public totalSupply;
    address public owner;

    mapping(address => uint256) public balanceOf;
    mapping(address => mapping(address => uint256)) public allowance;

    event Transfer(address indexed from, address indexed to, uint256 value);
    event Approval(
        address indexed owner,
        address indexed spender,
        uint256 value
    );

    /// @param initialHolder receives full supply (typically Hub)
    constructor(address initialHolder, uint256 initialSupply_) {
        require(initialHolder != address(0), "zero holder");
        require(initialSupply_ > 0, "zero supply");

        owner = initialHolder;
        totalSupply = initialSupply_;
        balanceOf[initialHolder] = initialSupply_;

        emit Transfer(address(0), initialHolder, initialSupply_);
    }

    function transfer(address to, uint256 amount) external returns (bool) {
        _transfer(msg.sender, to, amount);
        return true;
    }

    function approve(address spender, uint256 amount) external returns (bool) {
        allowance[msg.sender][spender] = amount;
        emit Approval(msg.sender, spender, amount);
        return true;
    }

    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool) {
        uint256 allowed = allowance[from][msg.sender];
        require(allowed >= amount, "allowance");
        if (allowed != type(uint256).max) {
            allowance[from][msg.sender] = allowed - amount;
        }
        _transfer(from, to, amount);
        return true;
    }

    function _transfer(address from, address to, uint256 amount) internal {
        require(to != address(0), "zero to");
        require(balanceOf[from] >= amount, "balance");
        balanceOf[from] -= amount;
        balanceOf[to] += amount;
        emit Transfer(from, to, amount);
    }
}
